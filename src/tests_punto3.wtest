import mundo.*
import jugadores.*
import hechizos.*
import artefactos.*
import refuerzos.*
import npc.*

describe "Tests sobre hechicería, lucha avanzada y NPCs" {

	const rolando
	const espectroMalefico
	const espadaDelDestino
	const unaMascara
	const unaArmadura
	const navi
	const ashleyGraham
	const mascaraClara
	
	fixture {
		rolando = new Jugador()
		espectroMalefico = new Logo(nombre = "espectro Malefico", multiplo = 1)
		mundo.fuerzaOscura(5)
		collarDivino.perlas(5)
		rolando.valorBaseLucha(1)
		rolando.hechizoPreferido(espectroMalefico)
		espadaDelDestino = new Arma()
		unaMascara = new MascaraOscura(indiceDeOscuridad = 1)
		unaArmadura = new Armadura(valorBase = 2)
		rolando.artefactos([ espadaDelDestino, collarDivino, unaMascara, unaArmadura, espejoFantastico ])
		navi = new Npc(nivel = facil, valorBaseLucha = 1)
		mascaraClara = new MascaraOscura(diasDeCompra = 0, peso = 2, indiceDeOscuridad = 0)
		ashleyGraham = new Npc(nivel = moderado, valorBaseLucha = 5, artefactos = [mascaraClara])
	}

	test "Rolando está cargado por tener 5 artefactos" {
		assert.that(rolando.estasCargado())
	}

	test "Rolando se saca la armadura y ya no está cargado" {
		rolando.desequipate(unaArmadura)
		assert.notThat(rolando.estasCargado())
	}

	test "Ronaldo tiene 20 puntos de habilidad de lucha" {
		assert.equals(rolando.habilidadDeLucha(), 20)
	}

	test "Si la armadura de ronaldo tiene una bendicion de refuerzo, su habilidad de lucha sera 58" {
		unaArmadura.refuerzo(bendicion)
		assert.equals(unaArmadura.unidadDeLucha(rolando), 58)
	}

	test "Si la armadura de ronaldo tiene una cota de malla de refuerzo, su habilidad de lucha sera 3" {
		const unaCotaDeMalla = new CotaDeMalla(refuerzo = 1)
		unaArmadura.refuerzo(unaCotaDeMalla)
		assert.equals(unaArmadura.unidadDeLucha(rolando), 3)
	}

	test "Si la armadura de ronaldo tiene un hechizo basico de refuerzo, su habilidad de lucha será de 12" {
		unaArmadura.refuerzo(hechizoBasico)
		assert.equals(unaArmadura.unidadDeLucha(rolando), 12)
	}

	test "Si la armadura de ronaldo tiene un espectro malefico de refuerzo, su habilidad de lucha sera de 19" {
		unaArmadura.refuerzo(espectroMalefico)
		assert.equals(unaArmadura.unidadDeLucha(rolando), 19)
	}

	test "Si ronaldo sólo tiene al espejo fantastico entre sus artefactos, su habilidad de lucha sera de 1" {
		rolando.desequipateTodo()
		rolando.equipate(espejoFantastico)
		assert.equals(rolando.habilidadDeLucha(), 1)
	}

	test "Si el hechizo preferido de ronaldo es un libro de hechizos con el espectro malefico y el hechizo basico, su nivelDeHechicería sera de 56" {
		const libroDeHechizos = new LibroDeHechizo(hechizos=[espectroMalefico,hechizoBasico])
		rolando.hechizoPreferido(libroDeHechizos)
		assert.equals(rolando.nivelDeHechiceria(), 56)
	}

	test "Si se agrega una armdura extra(valorBase=5) a Rolando con una cota de malla(valor=6) de refuerzo,su habilidad de lucha sera de 37" {
		const unaCotaDeMalla = new CotaDeMalla(refuerzo = 6)
		const otraArmadura = new Armadura(valorBase = 5)
		otraArmadura.refuerzo(unaCotaDeMalla)
		rolando.equipate(otraArmadura)
		assert.equals(rolando.habilidadDeLucha(), 37)
	}
	
	test "La habilidad de lucha del npc navi es de 1"{
		assert.equals(navi.habilidadDeLucha(), 1)
	}
	
	test "La habilidad de lucha del npc ashley graham es de 18"{
		assert.equals(ashleyGraham.habilidadDeLucha(), 18)
	}
	
	test "La dificultad de ashley graham ahora es dificil, su dificultad ahora es 36"{
		ashleyGraham.nivel(dificil)
		assert.equals(ashleyGraham.habilidadDeLucha(), 36)
	}

}


import artefactos.*
import hechizos.*
import mundo.*
import refuerzos.*
import jugadores.*

describe "Test sobre comercio" {

	const xenia
	const thor
	const loki
	const hechizoLogo
	const hechizoBasico

	fixture {
		xenia = new Jugador()
		thor = new Jugador()
		loki = new Jugador()
		hechizoBasico = new HechizoBasico()
		hechizoLogo = new Logo()
		xenia.hechizoPreferido(hechizoLogo)
		thor.hechizoPreferido(hechizoBasico)
		loki.hechizoPreferido(hechizoBasico)
		loki.monedasDeOro(5)
		hechizoLogo.nombre("alacachula cachicomula")
		hechizoLogo.multiplo(1)
	}

	test "Conocer el precio de lista de un hechizo de logo cuyo nombre es “alacachula cachicomula”." {
		assert.equals(hechizoLogo.precio(), 22)
	}

	test "Xenia canjea el hechizoLogo por un hechizo básico, Le quedan 100 monedas." {
		xenia.canjearHechizo(hechizoBasico)
		assert.equals(xenia.monedasDeOro(), 100)
	}

	test "Thor canjea el hechizo básico por el hechizoLogo, Le quedan 83 monedas." {
		thor.canjearHechizo(hechizoLogo)
		assert.equals(thor.monedasDeOro(), 83)
	}

	test "Thor canjea el hechizo básico 2 veces, Le quedan 90 monedas" {
		thor.canjearHechizo(hechizoBasico)
		thor.canjearHechizo(hechizoBasico)
		assert.equals(thor.monedasDeOro(), 90)
	}

	test "Loki quiere canjear el hechizo básico por el hechizoLogo, No deberia poder y se conserva su hechizo preferido" {
		loki.canjearHechizo(hechizoLogo)
		assert.equals(loki.hechizoPreferido(), hechizoBasico)
	}

	test "Xenia compra una espada, un collar divino(de 5 perlas), una armadura sin refuerzo y otra armadura con cota de malla cuyo valor de lucha es 5, Le quedan 70,5." {
		const espada = new Arma()
		const armadura = new Armadura(valorBase = 2)
		const malla = new CotaDeMalla(refuerzo = 5)
		const armaduraConCotaDeMalla = new Armadura(valorBase = 2, refuerzo = malla)
		collarDivino.perlas(5)
		xenia.comprarArtefactos([ espada, collarDivino, armadura, armaduraConCotaDeMalla ])
		assert.equals(xenia.monedasDeOro(), 70.5)
	}

	test "Xenia compra una espada, un collar divino(de 5 perlas), una armadura sin refuerzo y otra armadura con cota de malla cuyo valor de lucha es 5, tiene 4 artefactos" {
		const espada = new Arma()
		const armadura = new Armadura(valorBase = 2)
		const malla = new CotaDeMalla(refuerzo = 5)
		const armaduraConCotaDeMalla = new Armadura(valorBase = 2, refuerzo = malla)
		collarDivino.perlas(5)
		xenia.comprarArtefactos([ espada, collarDivino, armadura, armaduraConCotaDeMalla ])
		assert.equals(xenia.artefactos().size(), 4)
	}

	test "Thor compra una armadura con bendición como refuerzo, y una armadura reforzada con el hechizo alacachula, le quedan 74 monedas." {
		const armaduraBendita = new Armadura(valorBase = 2, refuerzo = bendicion)
		const armaduraHechizada = new Armadura(valorBase = 2, refuerzo = hechizoLogo)
		thor.comprarUnArtefacto(armaduraBendita)
		thor.comprarUnArtefacto(armaduraHechizada)
		assert.equals(thor.monedasDeOro(), 74)
	}

	test "Loki quiere comprar el espejo, no puede y no varian sus monedas" {
		loki.comprarUnArtefacto(espejoFantastico)
		assert.equals(loki.monedasDeOro(), 5)
	}

	test "Xenia canjea un libro de hechizos que contiene el hechizoLogo y el hechizo básico, Le quedan 69" {
		libroDeHechizos.hechizos([ hechizoLogo, hechizoBasico ])
		xenia.canjearHechizo(libroDeHechizos)
		assert.equals(xenia.monedasDeOro(), 69)
	}

}

